import org.apache.tools.ant.filters.ReplaceTokens

plugins {
	id 'org.jetbrains.kotlin.jvm' version '2.0.0'
	id 'io.github.goooler.shadow' version '8.1.7' apply false
}

allprojects {
	apply plugin: 'org.jetbrains.kotlin.jvm'

	archivesBaseName = 'VoteParty'
	group 'me.clip'
	version '2.36-DEV-Parallel'

	java {
		toolchain.languageVersion.set(JavaLanguageVersion.of(21))
	}

	kotlin {
		jvmToolchain(21)
	}

	repositories {
		mavenCentral()

		maven { url = "https://oss.sonatype.org/content/repositories/snapshots/" }
		maven { url = "https://repo.aikar.co/content/groups/aikar/" }
		maven { url = "https://papermc.io/repo/repository/maven-public/" }
		maven { url = "https://repo.extendedclip.com/content/repositories/placeholderapi/" }
		maven { url = "https://repo.glaremasters.me/repository/public/" }
	}

	dependencies {
		// kotlin
		implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"

		// spigot
		compileOnly "org.spigotmc:spigot-api:1.20.6-R0.1-SNAPSHOT"
	}

	sourceCompatibility = 21
	targetCompatibility = 21


	compileJava {

		options.release.set(21)
	}

	compileKotlin {

	}
}

apply plugin: 'io.github.goooler.shadow'

shadowJar {
	minimize()
	relocate("co.aikar.commands", "me.clip.voteparty.libs.acf")
	relocate("co.aikar.locales", "me.clip.voteparty.libs.locales")
	relocate("ch.jalu.configme", "me.clip.voteparty.libs.configme")
	relocate("org.inventivetalent", "me.clip.voteparty.libs.inventivetalent")
	relocate("net.kyori", "me.clip.voteparty.libs.kyori")
	relocate("com.cryptomorin.xseries", "me.clip.voteparty.libs.xseries")
	relocate("kotlin", "me.clip.voteparty.libs.kotlin")
	archiveFileName = "VoteParty-${project.version}.jar"
}


dependencies {

	// config
	implementation "ch.jalu:configme:1.4.1"

	// placeholderapi
	compileOnly "me.clip:placeholderapi:2.11.6"

	// NuVotifier hook
	compileOnly files("libs/nuvotifier-2.7.3.jar")

	// command handler
	implementation "co.aikar:acf-paper:0.5.1-SNAPSHOT"

	// Support for materials across all versions
	implementation 'com.github.cryptomorin:XSeries:10.0.0'

	// json stuff
	implementation 'net.kyori:adventure-platform-bukkit:4.1.1'
	implementation 'net.kyori:adventure-api:4.11.0'
	implementation 'net.kyori:adventure-text-minimessage:4.11.0'
	implementation project(':version')

	implementation project(":version_new")
	implementation project(":version_old")
}

processResources {
	filter ReplaceTokens, tokens: [version: version]
}
